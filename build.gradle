import com.android.ddmlib.DdmPreferences

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.ben-manes.versions'

apply from: "$rootDir/gradle/version.gradle"

group = 'com.github.tmurakami'
version = version_name

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        minSdkVersion 8
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [notClass: [
                'net.bytebuddy.agent.builder.ResettableClassFileTransformer',
                'net.bytebuddy.dynamic.loading.NoOpClassFileTransformer',
                'net.bytebuddy.jar.asm.commons.JSRInlinerAdapter',
                'net.bytebuddy.jar.asm.commons.TryCatchBlockSorter',
        ].join(',')]
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    DdmPreferences.setTimeOut 60000
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.android.support.test:runner:0.5'
    compile 'org.mockito:mockito-android:latest.release'
}
